#!/bin/sh

# The script following the big line produces a .bat file that will do the same (ish)
# on Windows as the install-cwb.sh script will on Unix.

# note that we need to convert the registry variable from Unix-escapes to Windows-escapes
# twice-over to reduce \\\\ to \
REGISTRY=${REGISTRY//\\\\/\\}
REGISTRY=${REGISTRY//\\\\/\\}

#################################################################################

cat <<__END__
@echo off

rem ## Directory tree where the CWB will be installed (normally %ProgramFiles%\CWB in Windows)
set PREFIX=${PREFIX}

rem Optionally change PREFIX...
goto choose_install_dir
:bf_choose_install_dir

rem ## You can also change individual installation directories ...

rem # CQP and other executable programs ("binaries")
set BINDIR=%PREFIX%\bin

rem # C header files (CL library and CQi specification)
set INCDIR=%PREFIX%\include

rem # CL library (can be linked into C programs) (!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!as dll?)
set LIBDIR=%PREFIX%\lib

rem # manual pages
set MANDIR=%PREFIX%\man

rem ############### DO NOT CHANGE ANYTHING BELOW THIS LINE ###############

rem We could use xcopy, but INSTALL is used for one file at a time...
set INSTALL=copy

set VERSION=${VERSION}
set REGISTRY=${REGISTRY}

set REALINCDIR=%INCDIR%\cwb
set REALMANDIR=%MANDIR%


rem # install CQP and utilities 

echo Installing CQP and utilities in %BINDIR%
if not exist "%BINDIR%" (
	mkdir "%BINDIR%"
)
for %%f in (bin\*.exe) do %INSTALL% "%%f" "%BINDIR%" > nul
for %%f in (bin\*.dll) do %INSTALL% "%%f" "%BINDIR%" > nul
echo ... done
echo.

rem # install CL library and header files

echo Installing CL library and header files in %LIBDIR% and %INCDIR%
if not exist "%LIBDIR%" (
	mkdir "%LIBDIR%"
)
if not exist "%REALINCDIR%" (
	mkdir "%REALINCDIR%"
)
rem libcl.a should, in theory, allow static linking if the linker used is mingw!
%INSTALL% lib\libcl.a "%LIBDIR%" > nul
%INSTALL% include\cwb\cl.h "%REALINCDIR%" > nul
%INSTALL% include\cwb\cqi.h "%REALINCDIR%" > nul
echo ... done
echo.

rem # install manpages

echo Installing manual pages in %MANDIR%
if not exist "%REALMANDIR%" (
	mkdir "%REALMANDIR%"
)
for %%f in (man\*.pdf) do %INSTALL% "%%f" "%REALMANDIR%" > nul
echo ... done

rem  # that's it!
echo Congratulations, the IMS Open Corpus Workbench v%VERSION% has been installed 
echo successfully.
echo.
echo Please make sure that 
echo      %BINDIR% 
echo is in your "path" so you can call CWB programs from the command prompt!
echo.
echo Also note that the compiled-in default corpus registry is 
echo      %REGISTRY%.
echo To use a different registry directory, use the -r flag when calling CWB 
echo programs.
echo.
goto:eof





:choose_install_dir

echo By default, CWB will be installed in 
echo      %PREFIX%
echo If you want to install it somewhere else, you should cancel this installation,
echo edit the file install-cwb-win.bat, and change the line "set PREFIX=..."
echo to contain the installation path of your choice.
echo.
echo Do you want to cancel installation?
echo.
set choice=
set /p choice=[Type Y or y then [ENTER] to cancel, anything else to continue]
if '%choice%'=='Y' goto:eof
if '%choice%'=='y' goto:eof

 
goto bf_choose_install_dir
__END__


